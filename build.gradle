plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group = "io.github.mrtjp"
archivesBaseName = "fabrication-engine"
version = System.getenv("AUTO_GENERATED_VERSION") ?: "0.0.0.1"

sourceSets {
    compiletest
}

configurations {
    compiletestImplementation.extendsFrom implementation
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation sourceSets.main.output

    compiletestImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    compiletestImplementation 'org.junit.platform:junit-platform-engine:1.8.2'
    compiletestImplementation sourceSets.main.output
}

test {
    useJUnitPlatform()
}

task compilerTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.compiletest.output.classesDirs
    classpath = sourceSets.compiletest.runtimeClasspath
}

jacocoTestReport {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

publishing {
    repositories {
        maven {
            url "https://nexus.covers1624.net/repository/maven-releases/"
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
    publications {
        fabricationEngine(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version

            from components.java

            pom {
                name = "fabrication-engine"
                description = "Simple compiler for tile-based logic circuits"

                //The publish plugin doesnt like GString's here apparently..
                url = "https://github.com/MrTJP/fabrication-engine".toString()
                scm {
                    url = "https://github.com/MrTJP/fabrication-engine".toString()
                    connection = "scm:git:git://github.com/MrTJP/fabrication-engine.git".toString()
                    connection = "scm:git:git@github.com:MrTJP/fabrication-engine.git".toString()
                }
                issueManagement {
                    system = 'github'
                    url = "https://github.com/MrTJP/fabrication-engine/issues".toString()
                }
                developers {
                    developer {
                        id = 'mrtjp'
                        name = 'mrtjp'
                    }
                }
            }
        }
    }
}
